// SPDX-License-Identifier: AGPL-3.0

pragma solidity 0.6.12;
pragma experimental ABIEncoderV2;

import {BaseStrategy} from "@yearnvaults/contracts/BaseStrategy.sol";
import {SafeERC20, SafeMath, IERC20, Address} from "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import {Math} from "@openzeppelin/contracts/math/Math.sol";

import "../interfaces/keeperDao.sol";
import "../interfaces/uniswap.sol";

contract Strategy is BaseStrategy {
    using SafeERC20 for IERC20;
    using Address for address;
    using SafeMath for uint256;

    IUniswapV2Router02 public uniswapRouter = IUniswapV2Router02(address(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D));
    IDistributeV1 private distributor = IDistributeV1(address(0xcadF6735144D1d7f1A875a5561555cBa5df2f75C));
    ILiquidityPoolV2 public pool = ILiquidityPoolV2(address(0x35fFd6E268610E764fF6944d07760D0EFe5E40E5));
    IERC20 public rook = IERC20(address(0xfA5047c9c78B8877af97BDcb85Db743fD7313d4a));
    IERC20 public weth = IERC20(address(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2));

    IKToken public kToken;

    address[] public path;

    // unsigned. Indicates the losses incurred from the protocol's deposit fees
    uint256 private incurredLosses = 0;

    constructor(address _vault) public BaseStrategy(_vault) {
        // You can set these parameters on deployment to whatever you want
        // maxReportDelay = 6300;
        // profitFactor = 100;
        // debtThreshold = 0;

        // check to see if KeeperDao can actually accept want (renBTC, DAI, USDC, ETH, WETH)
        kToken = pool.kToken(address(want));
        require(address(kToken) != address(0x0), "Protocol doesn't support this token!");
        want.safeApprove(address(pool), uint256(- 1));
        rook.safeApprove(address(uniswapRouter), uint256(- 1));
        kToken.approve(address(pool), uint256(- 1));

        if (address(want) == address(weth)) {
            path = [address(rook), address(weth)];
        } else {
            path = [address(rook), address(weth), address(want)];
        }
    }

    function name() external view override returns (string memory) {
        // Add your own name here, suggestion e.g. "StrategyCreamYFI"
        return 'StrategyKeeperDAOGeneric';
    }

    function estimatedTotalAssets() public view override returns (uint256) {
        return valueOfStaked().add(balanceOfUnstaked()).add(_estimateReward(balanceOfReward()));
    }

    function balanceOfUnstaked() public view returns (uint256){
        return want.balanceOf(address(this));
    }

    // valued in wants
    function valueOfStaked() public view returns (uint256){
        return pool.underlyingBalance(address(want), address(this));
    }

    // kTokens have different virtual prices. Balance != want
    function balanceOfStaked() public view returns (uint256){
        return kToken.balanceOf(address(this));
    }

    function balanceOfReward() public view returns (uint256){
        return rook.balanceOf(address(this));
    }

    function valueOfReward() public view returns (uint256){
        return _estimateReward(rook.balanceOf(address(this)));
    }

    // only way to find out is thru calculating a virtual price this way
    // TODO: return a reasonable value when div(0)
    function inKTokens(uint256 wantAmount) internal view returns (uint256){
        return wantAmount.mul(balanceOfStaked()).div(valueOfStaked());
    }

    function prepareReturn(uint256 _debtOutstanding) internal override returns (uint256 _profit, uint256 _loss, uint256 _debtPayment){
        // NOTE: Return `_profit` which is value generated by all positions, priced in `want`
        // NOTE: Should try to free up at least `_debtOutstanding` of underlying position

        // sell any reward
        uint256 rewards = balanceOfReward();
        if (rewards > 0) {
            _sell(rewards);
        }

        // from selling rewards
        uint256 profit = balanceOfUnstaked();
        uint256 usableProfit = 0;
        // this should be guaranteed if called by keeper. See {harvestTrigger()}
        if (profit > incurredLosses) {
            // we want strategy to pay off its own incurredLosses from deposit fees first so it doesn't have to report _loss to the vault
            usableProfit = profit.sub(incurredLosses);
            if (incurredLosses > 0) {
                pool.deposit(address(want), incurredLosses);
                incurredLosses = 0;
            }
        } else {
            _loss = incurredLosses.sub(profit);
        }

        // loss has been recorded. 
        incurredLosses = 0;

        if (_debtOutstanding > usableProfit) {
            // withdraw just enough to pay off debt
            uint256 _toWithdraw = Math.min(_debtOutstanding.sub(usableProfit), valueOfStaked());
            pool.withdraw(address(this), kToken, inKTokens(_toWithdraw));
            _debtPayment = Math.min(_debtOutstanding, balanceOfUnstaked());

        } else {
            _debtPayment = _debtOutstanding;
            _profit = usableProfit.sub(_debtOutstanding);
        }

        return (_profit, _loss, _debtPayment);
    }

    function adjustPosition(uint256 _debtOutstanding) internal override {
        // NOTE: Try to adjust positions so that `_debtOutstanding` can be freed up on *next* harvest (not immediately)
        // deposit any loose balance
        uint256 unstaked = balanceOfUnstaked();
        if (unstaked > 0) {
            _provideLiquidity(unstaked);
        }
    }

    // NAV premium of 0.64% when depositing everytime. Need to keep track of this for _loss calculation.
    function _provideLiquidity(uint256 _amount) private {
        uint256 stakedBefore = valueOfStaked();
        pool.deposit(address(want), _amount);
        uint256 stakedAfter = valueOfStaked();
        uint256 stakedDelta = stakedAfter.sub(stakedBefore);
        uint256 depositFee = _amount.sub(stakedDelta);
        uint256 oneAsBips = 10000;
        uint256 need = depositFee.mul(oneAsBips).div(oneAsBips - pool.depositFeeInBips());
        incurredLosses = incurredLosses.add(need);
    }

    function liquidatePosition(uint256 _amountNeeded) internal override returns (uint256 _liquidatedAmount, uint256 _loss) {
        // if _amountNeeded is more than currently available, need to unstake some and/or sell rewards to free up assets
        uint256 unstaked = balanceOfUnstaked();
        if (_amountNeeded > unstaked) {
            uint256 desiredWithdrawAmount = _amountNeeded.sub(unstaked);

            // can't withdraw more than staked
            uint256 actualWithdrawAmount = Math.min(desiredWithdrawAmount, valueOfStaked());

            pool.withdraw(address(this), kToken, inKTokens(actualWithdrawAmount));

            _liquidatedAmount = balanceOfUnstaked();
            // already withdrew all that it could, any difference left is considered _loss,
            // which is possible because of KeeperDao's initial deposit NAV premium of 0.64%
            // i.e initial 10,000 deposit -> 9,936 in pool. If withdraw immediately, _loss = 64
            _loss = _amountNeeded.sub(_liquidatedAmount);

        } else {
            // already have all of _amountNeeded unstaked
            _liquidatedAmount = _amountNeeded;
            _loss = 0;
        }

        return (_liquidatedAmount, _loss);
    }

    // NOTE: Can override `tendTrigger` and `harvestTrigger` if necessary
    // Since claiming reward is done asynchronously, rewards could sit idle in strategy before next harvest() call
    // See {claimRewards()} for detail
    function prepareMigration(address _newStrategy) internal override {
        kToken.transfer(_newStrategy, balanceOfStaked());
        rook.transfer(_newStrategy, balanceOfReward());
    }

    // Trigger harvest only if strategy has rewards, otherwise, there's nothing to harvest.
    // This logic is added on top of existing gas efficient harvestTrigger() in the parent class
    function harvestTrigger(uint256 callCost) public override view returns (bool) {
        return super.harvestTrigger(callCost) && balanceOfReward() > 0 && netPositive();
    }

    // Indicator for whether strategy has earned enough rewards to offset incurred losses.
    // Adding this to harvestTrigger() will ensure that strategy will never have to report a positive _loss to the vault and lower its trust
    function netPositive() internal view onlyKeepers returns (bool){
        return _estimateReward(balanceOfReward()) > incurredLosses;
    }

    // Has to be called manually since this requires off-chain data.
    // Needs to be called before harvesting otherwise there's nothing to harvest.
    function claimRewards(address _to, uint256 _earningsToDate, uint256 _nonce, bytes memory _signature) internal onlyKeepers {
        distributor.claim(_to, _earningsToDate, _nonce, _signature);
    }

    function harvest(address _to, uint256 _earningsToDate, uint256 _nonce, bytes memory _signature, bool claimFirst) external onlyKeepers {
        if (claimFirst) {
            claimRewards(_to, _earningsToDate, _nonce, _signature);
        }

        uint256 profit = 0;
        uint256 loss = 0;
        uint256 debtOutstanding = vault.debtOutstanding();
        uint256 debtPayment = 0;
        if (emergencyExit) {
            // Free up as much capital as possible
            uint256 totalAssets = estimatedTotalAssets();
            // NOTE: use the larger of total assets or debt outstanding to book losses properly
            (debtPayment, loss) = liquidatePosition(totalAssets > debtOutstanding ? totalAssets : debtOutstanding);
            // NOTE: take up any remainder here as profit
            if (debtPayment > debtOutstanding) {
                profit = debtPayment.sub(debtOutstanding);
                debtPayment = debtOutstanding;
            }
        } else {
            // Free up returns for Vault to pull
            (profit, loss, debtPayment) = prepareReturn(debtOutstanding);
        }

        // Allow Vault to take up to the "harvested" balance of this contract,
        // which is the amount it has earned since the last time it reported to
        // the Vault.
        debtOutstanding = vault.report(profit, loss, debtPayment);

        // Check if free returns are left, and re-invest them
        adjustPosition(debtOutstanding);

        emit Harvested(profit, loss, debtPayment, debtOutstanding);
    }

    function _sell(uint256 _amount) internal {
        // since claiming is async, no point in selling if strategy hasn't claimed rewards
        if (balanceOfReward() > 0) {
            uniswapRouter.swapExactTokensForTokens(_amount, uint256(0), path, address(this), now);
        }
    }

    function _estimateReward(uint256 _amount) public view returns (uint256){
        uint256 amountOut = 0;
        if (_amount > 0) {
            amountOut = uniswapRouter.getAmountsOut(_amount, path)[1];
        }
        return amountOut;
    }

    function protectedTokens() internal view override returns (address[] memory){
        address[] memory protected = new address[](2);
        protected[0] = address(kToken);
        protected[1] = address(rook);
        return protected;
    }

    receive() external payable {}
}
